use crate::day_02::ast;
use std::str::FromStr;

grammar;

pub Games: Vec<ast::Game> = {
    Game* => <>
}

Game: ast::Game = {
    "Game" <n:Num> ":" <r:Rounds> => ast::Game::new(n, r),
}

Rounds = Separated<Round, ";">;

Round: ast::Round = {
    Separated<Cube, ","> => ast::Round::from_cubes(<>),
};

Separated<T, Sep>:  Vec<T> = {
    <mut v:Separated<T, Sep>> Sep <e:T> => {
        v.push(e);
        v
    },
    T => vec![<>],
}

Cube: ast::Cube = {
    <n:Num> <c:Color> => ast::Cube(n, c)
};

Num: u32 = {
    r"[0-9]+" => u32::from_str(<>).unwrap()
};

Color: String = {
    <s:r"[a-zA-Z]+"> => s.to_lowercase()
};