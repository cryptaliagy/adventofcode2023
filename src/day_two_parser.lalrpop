use crate::day_two_ast;
use std::str::FromStr;

grammar;

pub Games: Vec<day_two_ast::Game> = {
    Game* => <>
}

Game: day_two_ast::Game = {
    "Game" <n:Num> ":" <r:Rounds> => day_two_ast::Game::new(n, r),
}

Rounds = Separated<Round, ";">;

Round: day_two_ast::Round = {
    Separated<Cube, ","> => day_two_ast::Round::from_cubes(<>),
};

Separated<T, Sep>:  Vec<T> = {
    <mut v:Separated<T, Sep>> Sep <e:T> => {
        v.push(e);
        v
    },
    T => vec![<>],
}
Cube: day_two_ast::Cube = {
    <n:Num> <c:Color> => day_two_ast::Cube(n, c)
};

Num: u32 = {
    r"[0-9]+" => u32::from_str(<>).unwrap()
};

Color: day_two_ast::ColorEnum = {
    "red" => day_two_ast::ColorEnum::Red,
    "blue" => day_two_ast::ColorEnum::Blue,
    "green" => day_two_ast::ColorEnum::Green,
};